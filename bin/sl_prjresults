#!/usr/bin/perl
use warnings;
use strict;
use Getopt::Long;
use Term::ANSIColor qw(colored);
use XML::LibXML;

my $skip = 1;
GetOptions("skip+"  => \$skip)
	or die("Error in command line arguments\n");

my $in;
{
	undef $/;
	$in = <>;
}

my $dom = XML::LibXML->load_xml(string => \$in);

my %ignore = (
	'disabled' => 1,
	'excluded' => 1,
	'succeeded' => 2,
	'blocked' => 3,
);

my %colors = (
	'succeeded' => 'green',
	'blocked' => 'grey10',
	'broken' => 'red',
	'building' => 'grey15',
	'failed' => 'red',
	'scheduled' => 'grey10',
	'unresolvable' => 'red',
);

foreach my $res ($dom->findnodes('/resultlist/result')) {
	my @out;
	foreach my $stat ($res->findnodes('./status')) {
		my $code = $stat->getAttribute('code');
		next if (defined $ignore{$code} && $ignore{$code} <= $skip);

		my $pkg = $stat->getAttribute('package');
		my $entry = "$pkg $code";
		$entry = colored($entry, $colors{$code}) if ($colors{$code});
		push @out, $entry;
	}

	next unless (scalar @out);

	my $repo = $res->getAttribute('repository');
	my $arch = $res->getAttribute('arch');

	print "$repo $arch\n";
	foreach my $o (@out) {
		print "\t$o\n";
	}
}

1;
